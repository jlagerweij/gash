apply plugin: 'java'
apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'


ext {
    sourceCompatibility = JavaVersion.VERSION_1_6   /* define the valid syntax level for source files */
    targetCompatibility = JavaVersion.VERSION_1_6   /* define binary compatiblity version */
}

repositories {
    mavenCentral()
    mavenRepo url: "lib"
}
configurations {
    provided
    onejar
    googlecode
}
dependencies {
    compile("com.gigaspaces:gs-openspaces:8.0.6") {
        exclude(module: 'openjpa-all')
    }
    compile "jline:jline:1.0"
    onejar 'com.simontuffs:one-jar-ant-task:0.97'
    googlecode 'ant-googlecode:ant-googlecode:0.0.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

jar { 
    manifest { 
        attributes 'Main-Class': 'net.lagerwey.gash.Gash' 
    } 
} 

task oneJar { 
    ext.destFile = file("${jar.destinationDir.path}/${project.archivesBaseName}-${version}-one-jar.jar")
    dependsOn jar 
    inputs.file file("${jar.destinationDir.path}/${project.archivesBaseName}-${version}.jar")
    outputs.file destFile
    doLast { 
        ant.taskdef(name: "onejar", classname: "com.simontuffs.onejar.ant.OneJarTask", classpath: configurations.onejar.asPath, onerror: "report") 
        ant.onejar(destfile: destFile) { 
            ant.main(jar: project.tasks.jar.archivePath) 
            ant.lib() { 
            configurations.runtime.findAll { !configurations.provided.contains(it) }.each { f -> 
                ant.fileset(file: f.path) 
            } 
            } 
        } 
    } 
} 

createReleaseTag.dependsOn uploadArchives

uploadArchives {
    dependsOn oneJar
    ext.versionWithoutSnapshot = version - '-SNAPSHOT'
    ext.archive = file("build/libs/gash-${version}-one-jar.jar")
    ext.targetfilename = "gash-${versionWithoutSnapshot}-one-jar.jar"
    ext.projectname = 'gash'
    ext.summary = "GigaSpaces Advanced Shell bundle ${version}"
    
    doLast {
        //acquire the user/password (usually supplied via command line)
        def googleUser = project.getProperty('googleUser')
        def googlePwd  = project.getProperty('googlePwd')

        assert googleUser && googlePwd : """Google upload not possible - googleUser or googlePwd property not provided.
        Pass -PgoogleUser=someone -PgooglePwd=secret via command line"""

        println summary
        assert archive.exists() : "" +
                "Google upload not possible - no binaries to upload. Please run 'gradlew build' first."

        logger.lifecycle "Uploading $archive ..."
        ant.taskdef(name:"gcupload", classname:"net.bluecow.googlecode.ant.GoogleCodeUploadTask", classpath: project.configurations.googlecode.asPath)
        ant.gcupload username:googleUser, password:googlePwd, projectname:projectname,
                filename:archive, targetfilename:targetfilename, summary:summary,
                labels:"Featured"
    }
}
